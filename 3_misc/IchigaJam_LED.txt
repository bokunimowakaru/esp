cls:new
rem     Espressif ESP8266 ESP-WROOM-02 ﾖｳ
rem     Wireless LED for IchigoJam Version 1.2
rem     Copyright (c) 2015-2016 Wataru KUNINO
rem     http://www.geocities.jp/bokunimowakaru/diy/ichigojam/

1 'ESP Example
2 uart 3:? "AT":wait 30
10 ? "AT+RESTORE":gosub 900
20 ? "AT+CWMODE=1":gosub 900
30 ? "AT+CWJAP=";chr$(34);
40 ? "ここにアクセスポイントのSSIDを記入する";
50 ? chr$(34);",";chr$(34);
60 ? "ここにパスワードを記入する";
70 ? chr$(34):wait 400:gosub 900
80 ? "AT+CIFSR":gosub 900
100 'Serv
110 ? "AT+CIPMUX=1":gosub 900
120 ? "AT+CIPSERVER=1,23":gosub 900
200 'LED
210 gosub 950:if K<>asc(":") goto 200
220 gosub 950:if K<>asc("L") goto 200
230 gosub 950:if K=asc("0") goto 250
240 led 1:beep:? "LED=1":goto 200
250 led 0:beep:? "LED=0":goto 200
260 goto 200
270 end
900 'Rx
910 wait 30:uart 0
920 K=inkey():if K=0 goto 940
930 ? chr$(K);:goto 920
940 uart 3:return
950 'Rx0
960 K=inkey():if K=0 goto 960
970 if K=33 uart 0:?:end
980 return
